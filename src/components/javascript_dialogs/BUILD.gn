# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

static_library("javascript_dialogs") {
  sources = [
    "app_modal_dialog_controller.cc",
    "app_modal_dialog_controller.h",
    "app_modal_dialog_manager.cc",
    "app_modal_dialog_manager.h",
    "app_modal_dialog_queue.cc",
    "app_modal_dialog_queue.h",
    "app_modal_dialog_view.h",
    "extensions_client.h",
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//components/strings",
    "//components/url_formatter",
    "//content/public/browser",
    "//content/public/common",
    "//net",
    "//skia",
    "//ui/base",
  ]

  if (use_aura) {
    deps += [ "//ui/aura" ]
  }

  if (toolkit_views) {
    sources += [
      "views/app_modal_dialog_view_views.cc",
      "views/app_modal_dialog_view_views.h",
    ]

    deps += [ "//ui/views" ]
  }

  if (is_android) {
    sources += [
      "android/javascript_app_modal_dialog_android.cc",
      "android/javascript_app_modal_dialog_android.h",
    ]

    deps += [
      "//components/javascript_dialogs/android:jni_headers",
      "//ui/android",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "app_modal_dialog_manager_unittest.cc" ]

  deps = [
    ":javascript_dialogs",
    "//base",
    "//testing/gtest",
  ]
}
